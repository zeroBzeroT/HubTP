plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'io.github.goooler.shadow' version '8.1.8'
}

group = 'lol.hub'
version = '4.0.3'
description = 'Minecraft self-service teleport requests'

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
        content {
            includeGroup 'com.mojang'
            includeGroup 'dev.folia'
            includeGroup 'io.papermc.paper'
            includeGroup 'net.kyori'
            includeGroup 'net.md-5'
        }
    }
    maven {
        // FoliaLib
        url = 'https://nexuslite.gcnt.net/repos/other/'
        content {
            includeGroup 'com.tcoded'
        }
    }
}

dependencies {
    // paper api
    // https://jd.papermc.io/paper/1.20/
    compileOnly group: 'io.papermc.paper', name: 'paper-api', version: "${project.minecraft_version}-R0.1-SNAPSHOT"

    // folia api
    // https://jd.papermc.io/folia/1.20/
    //compileOnly group: 'dev.folia', name: 'folia-api', version: "${project.minecraft_version}-R0.1-SNAPSHOT"

    // https://github.com/TechnicallyCoded/FoliaLib
    shadow group: 'com.tcoded', name: 'FoliaLib', version: "0.4.2"

    // Other dependencies
    shadow 'org.bstats:bstats-bukkit:3.1.0'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}
tasks.withType(JavaCompile).configureEach {
    it.sourceCompatibility = it.targetCompatibility = JavaVersion.toVersion(21)
    it.options.release = 21
    it.options.encoding = 'UTF-8'
}

processResources {
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        filter { line -> line.replace("@VERSION@", project.version) }
        filter { line -> line.replace("@DESCRIPTION@", project.description) }
    }
}

// https://imperceptiblethoughts.com/shadow/
shadowJar {
    configurations = [project.configurations.shadow]
    archiveFileName = "${project.name}-${project.version}+${project.minecraft_version}.jar"
    destinationDirectory = layout.buildDirectory.dir('dist')

    relocate 'org.bstats', 'com.krazzzzymonkey.cloudanarchycore.bstats'
    relocate 'com.tcoded', 'com.krazzzzymonkey.cloudanarchycore.tcoded'

    // delete unshaded jar
    doLast {
        file(jar.archiveFile).delete()
    }
}
build.finalizedBy(shadowJar)

tasks {
    runServer {
        runDirectory = layout.projectDirectory.dir('run')
        minecraftVersion(project.minecraft_version)
        dependsOn('build')
        systemProperty('com.mojang.eula.agree', 'true')
        downloadPlugins {
            url("https://github.com/ViaVersion/ViaBackwards/releases/download/5.3.2/ViaBackwards-5.3.2.jar")
            url("https://github.com/ViaVersion/ViaVersion/releases/download/5.3.2/ViaVersion-5.3.2.jar")
            url("https://github.com/dmulloy2/ProtocolLib/releases/download/5.3.0/ProtocolLib.jar")
        }
    }
}
